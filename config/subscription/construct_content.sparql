PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX terms: <http://purl.org/dc/terms/>

CONSTRUCT {
    ?agendapunt ?agendapunt_p ?agendapunt_o.
    ?zitting ?zitting_p ?zitting_o.
    ?behandeling ?behandeling_p ?behandeling_o.
    ?stemming ?stemming_p ?stemming_o.
    
    ?zitting besluit:behandelt ?agendapunt.
} WHERE {
    BIND(<CONTENT_URL> as ?agendapunt)
    ?agendapunt a besluit:Agendapunt.
    ?agendapunt ?agendapunt_p ?agendapunt_o.
    
    OPTIONAL {
        ?zitting a besluit:Zitting.
        ?zitting besluit:behandelt ?agendapunt.
        ?zitting ?zitting_p ?zitting_o.
        FILTER NOT EXISTS { ?zitting_o a besluit:Agendapunt. }
    }

    OPTIONAL {
        ?behandeling terms:subject ?agendapunt.
        ?behandeling ?behandeling_p ?behandeling_o.

        OPTIONAL {
            ?behandeling besluit:heeftStemming ?stemming.
            ?stemming ?stemming_p ?stemming_o.
        }
    }
}